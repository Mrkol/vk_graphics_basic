#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_EXT_debug_printf : enable

#define GROUP_SIZE 256

layout( local_size_x = GROUP_SIZE ) in;

 
layout(push_constant) uniform params_t
{
    mat4 mProjView;
    uint instanceCount;
    uint modelCount;
} params;

struct IndirectCall
{
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    int  vertexOffset;
    uint firstInstance;
};

layout(std430, binding = 0) buffer indirection_t
{
    IndirectCall indirections[];
};

layout(std430, binding = 1) buffer mapping_t
{
    uint mappings[];
};

struct InstanceInfo
{
    uint modelId;
    uint doRender;
};

layout(std430, binding = 2) buffer instance_infos_t
{
    InstanceInfo instanceInfos[];
};

layout(std430, binding = 3) buffer instance_matrices_t
{
    mat4 instanceMatrices[];
};

struct ModelInfo
{
    uint indexCount;
    uint indexOffset;
    uint vertexOffset;
    float AABB[6];
};

layout(std430, binding = 4) buffer model_infos_t
{
    ModelInfo modelInfos[];
};

// If there are more than 32K instances of a certain model, we are doomed
shared uint ourMapping[8192];
shared uint ourVisibleInstanceCount;
shared uint ourMappingStart;

void main()
{
    uint model_idx = gl_WorkGroupID.x;
    uint idx = gl_LocalInvocationID.x;
    
    if (idx == 0) { ourVisibleInstanceCount = 0; }

    // Is this necessary? Can't we synchronize the init above with fetch adds with memory barriers only?
    barrier();

    
        
    vec3 BBOX[8] = {
        vec3(modelInfos[model_idx].AABB[0], modelInfos[model_idx].AABB[1], modelInfos[model_idx].AABB[2]),
        vec3(modelInfos[model_idx].AABB[0], modelInfos[model_idx].AABB[1], modelInfos[model_idx].AABB[5]),
        vec3(modelInfos[model_idx].AABB[0], modelInfos[model_idx].AABB[4], modelInfos[model_idx].AABB[2]),
        vec3(modelInfos[model_idx].AABB[0], modelInfos[model_idx].AABB[4], modelInfos[model_idx].AABB[5]),
        vec3(modelInfos[model_idx].AABB[3], modelInfos[model_idx].AABB[1], modelInfos[model_idx].AABB[2]),
        vec3(modelInfos[model_idx].AABB[3], modelInfos[model_idx].AABB[1], modelInfos[model_idx].AABB[5]),
        vec3(modelInfos[model_idx].AABB[3], modelInfos[model_idx].AABB[4], modelInfos[model_idx].AABB[2]),
        vec3(modelInfos[model_idx].AABB[3], modelInfos[model_idx].AABB[4], modelInfos[model_idx].AABB[5])
        };
  
    for (uint i = idx; i < params.instanceCount; i += GROUP_SIZE)
    {
        if (instanceInfos[i].modelId != model_idx || instanceInfos[i].doRender == 0)
        {
            continue;
        }

        bool left = true;
        bool right = true;
        bool top = true;
        bool bottom = true;
        bool front = true;
        bool back = true;
        for (uint j = 0; j < 8; ++j)
        {
            vec4 screenspacePt = params.mProjView * instanceMatrices[i] * vec4(BBOX[j], 1.0f);
            screenspacePt /= screenspacePt.w;
            // if of AABB's vertices are on one side of a certain line,
            // all of it is on that side of the line
            // (lines are left-right-top-bottom of the screen)
            left = left && screenspacePt.x < -1;
            right = right && screenspacePt.x > 1;
            top = top && screenspacePt.y < -1;
            bottom = bottom && screenspacePt.y > 1;
            front = front && screenspacePt.z > 1;
            back = back && screenspacePt.z < 0;
        }

        if (left || right|| top || bottom || front || back)
        {
            continue;
        }


        // We do not need ordering of these adds between themselves
        uint mappingSlot = atomicAdd(ourVisibleInstanceCount, 1);
        
        ourMapping[mappingSlot] = i;
    }

    // Wait for all threads to complete their culling
    // also ensures that the subsequent ourVisibleInstanceCount read will see all atomic adds
    barrier();

    uint myVisibleInstanceCount = ourVisibleInstanceCount;

    if (idx == 0)
    {
        ourMappingStart = atomicAdd(mappings[0], myVisibleInstanceCount);
    }
    
    // Wait for thread 0 to get our mapping start
    // and ensure HB between the following ourMappingStart read and the previous write
    barrier();

    uint myMappingStart = ourMappingStart;

    for (uint i = idx; i < myVisibleInstanceCount; i += GROUP_SIZE)
    {
        uint myMapping = ourMapping[i];
        mappings[1 + ourMappingStart + i] = myMapping;
    }
    
    if (idx == 0)
    {
        indirections[model_idx].indexCount = modelInfos[model_idx].indexCount;
        indirections[model_idx].instanceCount = ourVisibleInstanceCount;
        indirections[model_idx].firstIndex = modelInfos[model_idx].indexOffset;
        indirections[model_idx].vertexOffset = int(modelInfos[model_idx].vertexOffset);
        indirections[model_idx].firstInstance = 1 + myMappingStart;
    }
}


